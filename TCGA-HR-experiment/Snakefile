from os.path import join

subworkflow dataworkflow:
    workdir:
        "../TCGA-data"

include: '../Snakefile'
include: '../SomaticSignatures/Snakefile'
include: '../signature-visualization/Snakefile'

OUTPUT_DIR = 'processed'
RAW_DIR = 'raw'

wildcard_constraints:
    cancer_type="BRCA"

# Files from the TCGA-data workflow
RAW_MUTATION_COUNT_MATRIX_FILE=join(RAW_DIR, "TCGA-{cancer_type}_mutation_count.tsv")
HR_FEATURE_FILE=join(OUTPUT_DIR, "TCGA-{cancer_type}_HRd_feature.tsv")
LST_FEATURE_FILE=join(OUTPUT_DIR, "TCGA-{cancer_type}_LST_feature.tsv")
HR_BIALLELIC_INACTIVATION_FILE=join(OUTPUT_DIR, "TCGA-{cancer_type}_HR-biallelic-inactivation_Riaz2017.tsv")
DDR_SILENCING_FILE=join(OUTPUT_DIR, "TCGA-{cancer_type}_Silencing_Knijnenburg2018.tsv")
CLINICAL_FILE=join(OUTPUT_DIR, "TCGA-{cancer_type}_clinical.tsv")

# Input files
MC_FILE=join(OUTPUT_DIR, "mutation-count-all_TCGA-{cancer_type}.tsv")
FEATURE_FILE=join(OUTPUT_DIR, "features-all_TCGA-{cancer_type}.tsv")

TEST_FEATURE_FILE=join(OUTPUT_DIR, "features-test_TCGA-{cancer_type}_{fold}.tsv")
TEST_MC_FILE=join(OUTPUT_DIR, "mutation-count-test_TCGA-{cancer_type}_{fold}.tsv")

TRAIN_FEATURE_FILE=join(OUTPUT_DIR, "features-train_TCGA-{cancer_type}_{fold}.tsv")
TRAIN_MC_FILE=join(OUTPUT_DIR, "mutation-count-train_TCGA-{cancer_type}_{fold}.tsv")

# OUTPUT FILES
TEST_EXPOSURES_FILE=join(OUTPUT_DIR, '{model}-test-normalized-exposures_{covariate_of_interest}_{covariates}_{K}_TCGA-{cancer_type}_{fold}.tsv')
TRAIN_EXPOSURES_FILE=join(OUTPUT_DIR, '{model}-train-normalized-exposures_{covariate_of_interest}_{covariates}_{K}_TCGA-{cancer_type}_{fold}.tsv')
STM_HELDOUT_LIKELIHOOD_FILE=join(OUTPUT_DIR, 'stm-heldout-likelihood_{covariates}_{K}_TCGA-{cancer_type}_{fold}.tsv')
STM_COMBINED_HELDOUT_LIKELIHOOD_FILE=join(OUTPUT_DIR, 'stm-combined-heldout_{covariates}_TCGA-{cancer_type}.tsv')
STM_ALL_NORMALIZED_EXPOSURES_FILE=join(OUTPUT_DIR, 'stm-all-normalized-exposures_{covariates}_{K}_TCGA-{cancer_type}.tsv')
STM_HELDOUT_LIKELIHOOD_RATIO_FILE=join(OUTPUT_DIR, 'stm-heldout-ratio_{covariate_of_interest}_{covariates}_{K}_TCGA-{cancer_type}_{fold}.tsv')
STM_EXOME_SIGNATURES_FILE=join(OUTPUT_DIR, 'stm-exome-signatures_{covariates}_{K}_TCGA-{cancer_type}.tsv')
HELDOUT_PR_FILE=join(OUTPUT_DIR, 'linear-svm-heldout-pr_normalized_{model}_{covariate_of_interest}_{covariates}_{K}_TCGA-{cancer_type}.tsv')
# Plot Files
LIKELIHOOD_PLOT=join(OUTPUT_DIR,'likelihood-plot_{covariates1}_{covariates2}_TCGA-{cancer_type}.pdf')
LST_PLOT=join(OUTPUT_DIR, "lst-scatterplot_{covariate_of_interest}_{covariates}_{K}_TCGA-{cancer_type}.pdf")
STM_BOXPLOT=join(OUTPUT_DIR, "stm-boxplot_{covariate_of_interest}_{covariates}_{K}_TCGA-{cancer_type}.pdf")
TOPIC_EXPOSURE_PLOT=join(OUTPUT_DIR, "stm-exposure-plot_{covariates}_{K}_TCGA-{cancer_type}_{topic}.pdf")
LST_COMBINED_HR_ALL_HR_LIKELIHOOD_PLOT=join(OUTPUT_DIR,'likelihood-plot_LST-combined-HR-all-HR_TCGA-BRCA.pdf')

# Model Files
SIGMA_FILE=join(OUTPUT_DIR, 'stm-sigma_{covariates}_{K}_TCGA-{cancer_type}.tsv')
GAMMA_FILE=join(OUTPUT_DIR, 'stm-gamma_{covariates}_{K}_TCGA-{cancer_type}.tsv')
COV_SIG_FILE=join(OUTPUT_DIR, 'stm-covariate-significance_{covariate_of_interest}_{covariates}_{K}_TCGA-{cancer_type}.tsv')

# Shuffled files
SHUFFLED_STM_EXOME_SIGNATURES_FILE=join(OUTPUT_DIR, 'stm-exome-signatures_{covariates}_{K}_TCGA-{cancer_type}_{iter_num}.tsv')
SHUFFLED_MC_FILE=join(OUTPUT_DIR, "mutation-count-all_TCGA-{cancer_type}_{iter_num}.tsv")
SHUFFLED_FEATURE_FILE=join(OUTPUT_DIR, "features-all_TCGA-{cancer_type}_{iter_num}.tsv")
SHUFFLED_SIGMA_FILE=join(OUTPUT_DIR, 'stm-sigma_{covariates}_{K}_TCGA-{cancer_type}_{iter_num}.tsv')
SHUFFLED_GAMMA_FILE=join(OUTPUT_DIR, 'stm-gamma_{covariates}_{K}_TCGA-{cancer_type}_{iter_num}.tsv')
SHUFFLED_COV_SIG_FILE=join(OUTPUT_DIR, 'stm-covariate-significance_{covariate_of_interest}_{covariates}_{K}_TCGA-{cancer_type}_{iter_num}.tsv')
EMP_COV_SIG_FILE=join(OUTPUT_DIR, 'stm-empirical-covariate-significance_{covariate_of_interest}_{covariates}_{K}_TCGA-{cancer_type}.tsv')

seed="123456"

N_FOLDS=5
FOLDS=range(0, N_FOLDS)

MIN_K=2
MAX_K=10
K_RANGE=range(MIN_K, MAX_K+1)
K=5

rule all:
    input:
        expand(SHUFFLED_STM_EXOME_SIGNATURES_FILE, cancer_type="BRCA", iter_num=range(0,5), covariates="BRCA1BRCA2RAD51C", K=K)

# Rules to generate figures in the paper
rule figure_2:
    input:
        # LIKELIHOOD_PLOT.format(covariates1="BRCA1BRCA2RAD51C", covariates2="NULL", cancer_type="BRCA"),
        #SIG_COSINE_PLOT.format(covariates="BRCA1BRCA2RAD51C", method="stm", K=K, project="TCGA-BRCA"),
        STM_BOXPLOT.format(covariates="BRCA1BRCA2RAD51C*nMuts", covariate_of_interest="BRCA1BRCA2RAD51C", cancer_type="BRCA", K=K),
        #LST_PLOT.format(covariates="BRCA1BRCA2RAD51C", covariate_of_interest="BRCA1BRCA2RAD51C", cancer_type="BRCA", K=K)


rule supplemental_figures_S1_S2:
    input:
        LST_COMBINED_HR_ALL_HR_LIKELIHOOD_PLOT,
        SIG_PLOT.format(covariates="BRCA1BRCA2RAD51C", method="stm", project="TCGA-BRCA", K=K)

rule supplemental_figures_S3:
    input:
        SS_NUM_SIG_PLOT.format(project="TCGA-BRCA"),

rule calculate_covariate_significance:
    input:
        EMP_COV_SIG_FILE.format(covariates="BRCA1BRCA2RAD51C", covariate_of_interest="BRCA1BRCA2RAD51C", cancer_type="BRCA", K=K)

rule eval_HR_prediction_stm:
    input:
        expand(HELDOUT_PR_FILE, model="stm", covariate_of_interest="BRCA1BRCA2RAD51C", covariates="BRCA1BRCA2RAD51C+nMuts", K=K, cancer_type="BRCA"),
        expand(HELDOUT_PR_FILE, model="stm", covariate_of_interest="BRCA1BRCA2RAD51C", covariates="NULL", K=K, cancer_type="BRCA"),

rule eval_HR_prediction_SS:
    input:
        expand(HELDOUT_PR_FILE, model="SS", covariate_of_interest="NULL", covariates="NULL", K=K, cancer_type="BRCA")


rule eval_prediction_using_PR:
    input:
        expand(TRAIN_EXPOSURES_FILE, model="{model}", covariates="{covariates}", covariate_of_interest="{covariate_of_interest}", K="{K}", fold=FOLDS, cancer_type="{cancer_type}"),
        expand(TEST_EXPOSURES_FILE, model="{model}", covariates="{covariates}", covariate_of_interest="{covariate_of_interest}", K="{K}", fold=FOLDS, cancer_type="{cancer_type}"),
        expand(TRAIN_FEATURE_FILE, covariates="{covariates}", K="{K}", fold=FOLDS, cancer_type="{cancer_type}"),
        expand(TEST_FEATURE_FILE, covariates="{covariates}", K="{K}", fold=FOLDS, cancer_type="{cancer_type}")
    output:
        HELDOUT_PR_FILE
    script:
        "src/run_LinearSVC_PR.py"

rule covariate_empirical_significance:
    input:
        COV_SIG_FILE.format(covariates="{covariates}", covariate_of_interest="{covariate_of_interest}", cancer_type="{cancer_type}", K="{K}"),
        expand(SHUFFLED_COV_SIG_FILE, covariates="{covariates}", covariate_of_interest="{covariate_of_interest}", cancer_type="{cancer_type}", K="{K}", iter_num=range(0,100)),
    output:
        EMP_COV_SIG_FILE#.format(covariates="BRCA1BRCA2RAD51C", covariate_of_interest="BRCA1BRCA2RAD51C", cancer_type="BRCA", K=K)
    script:
        "../src/calculate_empirical_covariate_significance.py"


rule shuffle_covariates:
    params:
        seed=seed
    input:
        MC_FILE,
        FEATURE_FILE,
    output:
        SHUFFLED_MC_FILE,
        SHUFFLED_FEATURE_FILE
    script:
        "../src/shuffle_covariates.py"


rule shuffled_covariate_significance:
    input:
        SHUFFLED_SIGMA_FILE,
        SHUFFLED_GAMMA_FILE,
        SHUFFLED_FEATURE_FILE
    params:
        seed=seed
    output:
        SHUFFLED_COV_SIG_FILE
    script:
        "../src/simulate_logistic_normal.py"

rule covariate_significance:
    input:
        SIGMA_FILE,
        GAMMA_FILE,
        FEATURE_FILE
    params:
        seed=seed
    output:
        COV_SIG_FILE
    script:
        "../src/simulate_logistic_normal.py"

# plot likelihood of TCSM w/ LST vs. w/ BRCA1/2+RAD51C vs. w/ all 7 HR genes
rule plot_LST_all_HR_combined_HR_likelihoods:
    input:
        expand(STM_COMBINED_HELDOUT_LIKELIHOOD_FILE, cancer_type="BRCA",
               covariates=["LST", "BRCA1BRCA2RAD51C", "HRINACTIVATIONS"],
               K=K)
    output:
        LST_COMBINED_HR_ALL_HR_LIKELIHOOD_PLOT
    script:
        "../src/plot_likelihoods.py"


rule plot_topic_exposures:
    input:
        STM_ALL_NORMALIZED_EXPOSURES_FILE,
        FEATURE_FILE
    output:
        TOPIC_EXPOSURE_PLOT
    script:
        "src/plot_exposures.py"

rule plot_heldout_ratio:
    input:
        expand(STM_HELDOUT_LIKELIHOOD_RATIO_FILE, covariates="{covariates}", covariate_of_interest="{covariate_of_interest}", fold=FOLDS, K="{K}", cancer_type="{cancer_type}")
    output:
        STM_BOXPLOT
    script:
        "src/plot_heldout_ratio.py"

rule plot_LST_by_heldout_ratio:
    input:
        expand(STM_HELDOUT_LIKELIHOOD_RATIO_FILE, covariates="{covariates}", covariate_of_interest="{covariate_of_interest}", fold=FOLDS, K="{K}", cancer_type="{cancer_type}")
    output:
        LST_PLOT
    script:
        "src/plot_LST.py"

rule combine_stm_heldout_likelihood_files:
    input:
        expand(STM_HELDOUT_LIKELIHOOD_FILE, K = K_RANGE,
               cancer_type="{cancer_type}", fold=FOLDS, covariates="{covariates}")
    output:
        STM_COMBINED_HELDOUT_LIKELIHOOD_FILE
    script:
        "../src/combine_heldout_likelihood_files.py"


rule split_test_training_data:
    input:
        MC_FILE,
        FEATURE_FILE
    params:
        seed=seed,
        n_folds=N_FOLDS,
        feature="HR Status"
    output:
        TEST_MC_FILE,
        TRAIN_MC_FILE,
        TEST_FEATURE_FILE,
        TRAIN_FEATURE_FILE
    script:
        "../src/stratified_split_test_training_data.py"


rule clean_feature_mutation_count:
    params:
        seed=seed
    input:
        dataworkflow(RAW_MUTATION_COUNT_MATRIX_FILE),
        dataworkflow(HR_BIALLELIC_INACTIVATION_FILE),
        dataworkflow(DDR_SILENCING_FILE),
        dataworkflow(LST_FEATURE_FILE),
        dataworkflow(HR_FEATURE_FILE),
        dataworkflow(CLINICAL_FILE),
    output:
        MC_FILE,
        FEATURE_FILE
    script:
        "src/clean_feature_mutation_count.py"
