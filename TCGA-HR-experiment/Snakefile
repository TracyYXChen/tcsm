from os.path import join

include: '../Snakefile'
#include: '../SomaticSignatures/Snakefile'
include: '../signature-visualization/Snakefile'

OUTPUT_DIR = 'processed'
RAW_DIR = 'raw'

wildcard_constraints:
    cancer_type="BRCA|OV"


# Raw input files
SBS96_COUNT_URL = "https://obj.umiacs.umd.edu/mutation-signature-explorer/mutations/PanCanAtlas/processed/counts/counts.TCGA-{cancer_type}_{cancer_type}_mc3.v0.2.8.SBS-96.tsv"
RAW_HR_BIALLELIC_INACTIVATION_FILE=join(RAW_DIR, "TCGA-{cancer_type}_HR-biallelic-inactivation_Riaz2017.tsv")
RAW_DDR_SILENCING_FILE=join(RAW_DIR, "TCGA-{cancer_type}_Silencing_Knijnenburg2018.tsv")
RAW_LST_FILE=join(RAW_DIR, "TCGA-{cancer_type}_LST_Riaz2017.tsv")

RAW_MUTATION_COUNT_MATRIX_FILE=join(RAW_DIR, "TCGA-{cancer_type}_mutation_count.tsv")

ALL_MUTATION_COUNT_MATRIX_FILE=join(OUTPUT_DIR, "TCGA-{cancer_type}_all_mutation_count.tsv")
ALL_FEATURE_FILE=join(OUTPUT_DIR, "TCGA-{cancer_type}_all_clinical_features.tsv")

MC_FILE=join(OUTPUT_DIR, "TCGA-{cancer_type}_mutation_count.tsv")
FEATURE_FILE=join(OUTPUT_DIR, "TCGA-{cancer_type}_features.tsv")

VALIDATE_FEATURE_FILE=join(OUTPUT_DIR, "TCGA-{cancer_type}_mutation_count_validate.tsv")
VALIDATE_MC_FILE=join(OUTPUT_DIR, "TCGA-{cancer_type}_clinical_features_validate.tsv")

TEST_FEATURE_FILE=join(OUTPUT_DIR, "features-test_TCGA-{cancer_type}_{fold}.tsv")
TEST_MC_FILE=join(OUTPUT_DIR, "mutation-count-test_TCGA-{cancer_type}_{fold}.tsv")

TRAIN_FEATURE_FILE=join(OUTPUT_DIR, "features-train_TCGA-{cancer_type}_{fold}.tsv")
TRAIN_MC_FILE=join(OUTPUT_DIR, "mutation-count-train_TCGA-{cancer_type}_{fold}.tsv")



#STM_BOXPLOT=join(OUTPUT_DIR, "stm-boxplot_{K}_{project}.pdf")

# OUTPUT FILES
STM_HELDOUT_LIKELIHOOD_FILE=join(OUTPUT_DIR, 'stm-heldout-likelihood_{covariates}_{K}_TCGA-{cancer_type}_{fold}.tsv')
STM_COMBINED_HELDOUT_LIKELIHOOD_FILE=join(OUTPUT_DIR, 'stm-combined-heldout_{covariates}_TCGA-{cancer_type}.tsv')

LIKELIHOOD_PLOT=join(OUTPUT_DIR,'likelihood-plot_{covariates1}_{covariates2}_TCGA-{cancer_type}.pdf')
STM_HELDOUT_LIKELIHOOD_RATIO_FILE=join(OUTPUT_DIR, 'stm-heldout-ratio_{covariates}_{K}_TCGA-{cancer_type}_{fold}.tsv')
STM_BOXPLOT=join(OUTPUT_DIR, "stm-boxplot_{K}_TCGA-{cancer_type}.pdf")


seed="123456"

N_FOLDS=5
FOLDS=range(0, N_FOLDS)

MIN_K=2
MAX_K=10
K_RANGE=range(MIN_K, MAX_K+1)


rule all:
    input:
        expand(ALL_FEATURE_FILE, cancer_type="BRCA")
        # expand(SIG_COSINE_PLOT, covariates="BRCA12", K=7, project="TCGA-BRCA")


rule calc_stats_ratio:
    input:
        expand(STM_HELDOUT_LIKELIHOOD_RATIO_FILE, covariates="BRCA12", fold=FOLDS, K="{K}", cancer_type="{cancer_type}")
    output:
        STM_BOXPLOT
    script:
        "src/calculate_heldout_ratio_statistics.py"


rule combine_stm_heldout_likelihood_files:
    input:
        expand(STM_HELDOUT_LIKELIHOOD_FILE, K = K_RANGE,
               cancer_type="{cancer_type}", fold=FOLDS, covariates="{covariates}")
    output:
        STM_COMBINED_HELDOUT_LIKELIHOOD_FILE
    script:
        "../src/combine_heldout_likelihood_files.py"


rule split_test_training_data:
    input:
        MC_FILE,
        FEATURE_FILE
    params:
        seed=seed,
        n_folds=N_FOLDS
    output:
        TEST_MC_FILE,
        TRAIN_MC_FILE,
        TEST_FEATURE_FILE,
        TRAIN_FEATURE_FILE
    script:
        "../src/split_test_training_data.py"


rule split_validate_training:
    input:
        ALL_MUTATION_COUNT_MATRIX_FILE,
        ALL_FEATURE_FILE
    params:
        seed=seed
    output:
        VALIDATE_MC_FILE,
        MC_FILE,
        VALIDATE_FEATURE_FILE,
        FEATURE_FILE
    script:
        "../src/split_training_validate_data.py"

rule download_sbs_count_data:
    params:
        SBS96_COUNT_URL
    output:
        RAW_MUTATION_COUNT_MATRIX_FILE
    shell:
        "wget -O {output} {params}"

rule clean_feature_mutation_count:
    input:
        RAW_MUTATION_COUNT_MATRIX_FILE,
        RAW_HR_BIALLELIC_INACTIVATION_FILE,
        RAW_DDR_SILENCING_FILE,
        RAW_LST_FILE
    output:
        ALL_MUTATION_COUNT_MATRIX_FILE,
        ALL_FEATURE_FILE
    script:
        "src/clean_feature_mutation_count.py"
