from os.path import join

OUTPUT_DIR = 'processed'
RAW_DIR = 'raw'

wildcard_constraints:
    cancer_type="BRCA|OV"


# Raw input files
SBS96_COUNT_URL = "https://obj.umiacs.umd.edu/mutation-signature-explorer/mutations/PanCanAtlas/processed/counts/counts.TCGA-{cancer_type}_{cancer_type}_mc3.v0.2.8.SBS-96.tsv"
RAW_HR_BIALLELIC_INACTIVATION_FILE=join(RAW_DIR, "TCGA-{cancer_type}_HR-biallelic-inactivation_Riaz2017.tsv")
RAW_DDR_SILENCING_FILE=join(RAW_DIR, "TCGA-{cancer_type}_Silencing_Knijnenburg2018.tsv")
RAW_LST_FILE=join(RAW_DIR, "TCGA-{cancer_type}_LST_Riaz2017.tsv")
RAW_POLE_FILE=join(RAW_DIR, "classify_POLE_in_TCGA-{cancer_type}_mc3.v0.2.8.SBS.tsv")
RAW_MUTATION_COUNT_MATRIX_FILE=join(RAW_DIR, "TCGA-{cancer_type}_mutation_count.tsv")
# Processed files
POLE_EXO_FILE=join(OUTPUT_DIR, "TCGA-{cancer_type}_POLE-exo_feature.tsv")
HR_FEATURE_FILE=join(OUTPUT_DIR, "TCGA-{cancer_type}_HRd_feature.tsv")
LST_FEATURE_FILE=join(OUTPUT_DIR, "TCGA-{cancer_type}_LST_feature.tsv")
HR_BIALLELIC_INACTIVATION_FILE=join(OUTPUT_DIR, "TCGA-{cancer_type}_HR-biallelic-inactivation_Riaz2017.tsv")
DDR_SILENCING_FILE=join(OUTPUT_DIR, "TCGA-{cancer_type}_Silencing_Knijnenburg2018.tsv")


rule all:
    input:
        expand(POLE_EXO_FILE, cancer_type="BRCA"),
        expand(HR_FEATURE_FILE, cancer_type="BRCA"),
        expand(LST_FEATURE_FILE, cancer_type="BRCA"),
        expand(HR_BIALLELIC_INACTIVATION_FILE, cancer_type="BRCA"),
        expand(DDR_SILENCING_FILE, cancer_type="BRCA"),

rule clean_pole_file:
    input:
        RAW_POLE_FILE,
        RAW_MUTATION_COUNT_MATRIX_FILE
    output:
        POLE_EXO_FILE
    script:
        "src/clean_pole_mutations.py"

rule clean_biallelic_hr_inactivation:
    input:
        RAW_HR_BIALLELIC_INACTIVATION_FILE
    output:
        HR_BIALLELIC_INACTIVATION_FILE
    script:
        "src/clean_hr_biallelic_inactivations.py"

rule clean_epigenetic_silencing_file:
    input:
        RAW_DDR_SILENCING_FILE
    output:
        DDR_SILENCING_FILE
    script:
        "src/clean_epigenetic_silencing.py"

rule combine_hr_file:
    input:
        RAW_HR_BIALLELIC_INACTIVATION_FILE,
        RAW_DDR_SILENCING_FILE
    output:
        HR_FEATURE_FILE
    script:
        "src/combine_hr_inactivations.py"

rule clean_lst_file:
    input:
        RAW_LST_FILE
    output:
        LST_FEATURE_FILE
    script:
        "src/clean_lst_file.py"

rule download_sbs_count_data:
    params:
        SBS96_COUNT_URL
    output:
        RAW_MUTATION_COUNT_MATRIX_FILE
    shell:
        "wget -O {output} {params}"
